buildscript {

	ext {
		springBootVersion = '1.5.9.RELEASE'
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE")
		classpath("se.transmode.gradle:gradle-docker:1.2")
	}

}

subprojects {

	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: "io.spring.dependency-management"


	group = 'com.typesafe.microservices-experiments'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom 'io.spring.platform:platform-bom:Brussels-SR6'
		}
	}

	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}

		repositories{
			mavenLocal()
		}
	}

}

configure([project(':order-service'),
		   project(':product-service'),
		   project(':account-service'),
		   project(':zipkin'),
		   project(':eureka'),
		   project(':zuul')]) {


	apply plugin: 'org.springframework.boot'
	apply plugin: "docker"


	ext {
		springCloudVersion = 'Camden.SR7'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	task buildDocker(type: Docker, dependsOn: build) {

		def projectName = "microservices-experiments"
		def jarName = jar.baseName.toLowerCase()
		maintainer = "zouabi.mourad@gmail.com"

		tag = "$projectName/$jarName"
		dockerfile = file('./Dockerfile')
		doFirst {
			copy {
				from jar
				into stageDir
			}
		}
	}

}